
use_linux_msgloop = false

shared_library("uisdk") {
    cflags = []
    if (is_debug) {
      cflags += ["-g"]
    }

    if (use_linux_msgloop) {
        defines = ["USE_LINUX_MSGLOOP"]
    }

    include_dirs = [
        "//",
        "//ui/sdk",
        "//ui/sdk/include",
        "//ui/sdk/src",
        "//ui/3rd/skia/",
        "//ui/3rd/skia/include/core/"
    ]

    lib_dirs = ["../3rd/skia/lib"]
    libs = ["skia"]

    cflags += exec_script("//build/pkg-config.py", ["--cflags", "glib-2.0"], "value")
    sources = [
        "src/message_loop/message_loop.cpp",
        "src/application/uiapplication.cpp",
        "src/application/iuiapplication.cpp",
        "src/window/window.cpp",
        "src/window/iwindow.cpp",
        "src/object/message.cpp",
        "src/object/imessage.cpp",
        "src/util/log/log.cpp",
    ]
    if (is_linux || use_linux_msgloop) {
        sources += [
            "src/window/window_linux.cpp",
            "src/window/linux/display.cpp",
            "src/message_loop/message_loop_linux.cpp",
        ]
        libs += [
            "glib-2.0",
            "x11"
        ]
    }
    if (is_mac && !use_linux_msgloop) {
        sources += [
            "src/window/window_mac.mm",
            "src/message_loop/message_loop_mac.mm",
        ]
    }

    if (is_mac) {
        sources += [
            "src/application//application_mac.mm",
        ]
        # skia Mac 依赖
        ldflags = [
            "-framework", "CoreFoundation",
            "-framework", "CoreGraphics",
            "-framework", "CoreText",
            "-framework", "CoreServices",
            "-framework", "Cocoa",
        ]
    }
}

group("sdk") {
    deps = [
        ":uisdk"
    ]
}

#######################################

config("unittest_common") {
  if (is_debug) {
    cflags = ["-g"]
  }
}

executable("signalslot_unittest") {
    include_dirs = [
        "//ui/sdk/include",
    ]
    public_configs = [":unittest_common"]
    sources = ["include/common/signalslot/signalslot_unittest.cpp"]
}
executable("weakptr_unittest") {
    include_dirs = [
        "//ui/sdk/include",
    ]
    public_configs = [":unittest_common"]
    sources = ["include/common/weakptr/weakptr_unittest.cpp"]
}
executable("message_loop_unittest") {
    include_dirs = [
        "//ui/sdk/include",
    ]
    if (use_linux_msgloop) {
        defines = ["USE_LINUX_MSGLOOP"]
    }

    public_configs = [":unittest_common"]
    libs = ["glib-2.0"]
    cflags = exec_script("//build/pkg-config.py", ["--cflags", "glib-2.0"], "value")
    sources = [
        "src/message_loop/message_loop.cpp",
        "src/message_loop/message_loop_unittest.cpp"
    ]
    if (is_linux || use_linux_msgloop) {
        sources += [
            "src/window/linux/display.cpp",
            "src/message_loop/message_loop_linux.cpp"
            ]
        libs += [
            "glib-2.0",
            "x11"
        ]
    }
    else if (is_mac) {
        sources += [
            "src/message_loop/message_loop_mac.mm",
        ]
    }


    if (is_mac) {
        sources += [
            "src/application/application_mac.mm",
        ]
        # skia Mac 依赖
        ldflags = [
            "-framework", "CoreFoundation",
            "-framework", "CoreGraphics",
            "-framework", "CoreText",
            "-framework", "CoreServices",
            "-framework", "Cocoa",
        ]
    }
}

group("unittest") {
    deps = [
        ":signalslot_unittest",
        ":weakptr_unittest",
        ":message_loop_unittest"
    ]
}