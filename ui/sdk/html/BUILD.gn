
source_set("html") {
  deps=[
    ":gen_property_id_hash",
    ":gen_value_id_hash",
    ":gen_atrule_descriptor_id_hash",
  ]

  sources = [
    "core_initializer.h",
    "core_initializer.cpp",

    "base/memory.h",
    "base/memory.cpp",
    "base/atomic_string.h",
    "base/atomic_string.cpp",

    "dom/qualified_name.h",
    "dom/qualified_name.cpp",
    "dom/node.h",
    "dom/element.h",
    "dom/element.cpp",

    "css/css_selector.h",
    "css/css_selector.cpp",
    "css/css_property_value_set.h",
    "css/css_property_value_set.cpp",
    "css/css_default_style_sheets.h",
    "css/css_default_style_sheets.cpp",
    "css/rule_set.h",
    "css/rule_set.cpp",
    "css/style_sheet_contents.h",
    "css/style_sheet_contents.cpp",

    "css/parser/css_at_rule_id.cpp",
    "css/parser/css_at_rule_id.h",
    "css/parser/css_parser_token.h",
    "css/parser/css_parser_token.cpp",
    "css/parser/css_parser_tokenizer.h",
    "css/parser/css_parser_tokenizer.cpp",
    "css/parser/css_parser_token_stream.h",
    "css/parser/css_parser_token_stream.cpp",
    "css/parser/css_property_parser.h",
    "css/parser/css_property_parser.cpp",
    "css/parser/css_parser.h",
    "css/parser/css_parser.cpp",
    "css/parser/css_selector_parser.cpp",
    "css/parser/css_selector_parser.h",
    "css/parser/css_variable_parser.cpp",
    "css/parser/css_variable_parser.h",

    "css/resolver/style_resolver.h",
    "css/resolver/style_resolver.cpp",
    "css/resolver/style_resolver_state.h",
    "css/resolver/style_resolver_state.cpp",
    "css/resolver/match_request.h",
    "css/resolver/match_request.cpp",
    "css/resolver/element_resolve_context.h",
    "css/resolver/element_resolve_context.cpp",

    "css/property/css_property.h",
    "css/property/css_property.cpp",
    "css/property/css_property_instances.h",
    "css/property/css_property_instances.cpp",
    "css/property/css_parsing_utils.h",
    "css/property/css_parsing_utils.cpp",
    "css/property/css_value.h",
    "css/property/css_value.cpp",

    "css/property/property_id.h",
    "css/property/property_id.cpp",
    "$target_gen_dir/css/property/property_id_enum.h",
    "$target_gen_dir/css/property/property_id_hash.cpp",

    "css/property/value_id.h",
    "css/property/value_id.cpp",
    "$target_gen_dir/css/property/value_id_enum.h",
    "$target_gen_dir/css/property/value_id_hash.cpp",

    "css/parser/at_rule_descriptor_id.h",
    "css/parser/at_rule_descriptor_id.cpp",
    "$target_gen_dir/css/parser/at_rule_descriptor_id_enum.h",
    "$target_gen_dir/css/parser/at_rule_descriptor_id_hash.cpp",

    "css/graphics/color.h",
    "css/graphics/color.cpp",
    "css/style_property_shorthand.h",
    "css/style_property_shorthand.cpp",
    "css/css_variable_data.cpp",
    "css/css_variable_data.h",

    "util/util.h",
    "util/util.cpp",
  ]

  if (is_debug) {
    sources += [
      "test/html_unittest_css_parser_token.cpp",
      "test/html_unittest_css_parser.cpp",
      "test/html_unittest_css_property.cpp",
      "test/html_unittest_memory.cpp",
      "test/html_unittest.cpp",
    ]
  }

  include_dirs=[
    "//ui/sdk",
    "$target_gen_dir/../",
  ]
}

# 生成property name->id 的完美Hash
action("gen_property_id_hash") {
  script="perfect_hash.py"
  inputs = [
    "css/property/property_id.json5",
  ]
  args = [
    rebase_path(inputs[0]),  # input file path
    rebase_path("$target_gen_dir/css/property")
  ]
  outputs = [
    "$target_gen_dir/css/property/property_id_enum.h",
    "$target_gen_dir/css/property/property_id_hash.template",
    "$target_gen_dir/css/property/property_id_hash.cpp",
  ]
}
# 生成property value->id 的完美Hash
action("gen_value_id_hash") {
  script="perfect_hash.py"
  inputs = [
    "css/property/value_id.json5",
  ]
  args = [
    rebase_path(inputs[0]),  # input file path
    rebase_path("$target_gen_dir/css/property")
  ]
  outputs = [
    "$target_gen_dir/css/property/value_id_enum.h",
    "$target_gen_dir/css/property/value_id_hash.template",
    "$target_gen_dir/css/property/value_id_hash.cpp",
  ]
}

# 生成AtRuleDescriptorID value->id 的完美Hash
action("gen_atrule_descriptor_id_hash") {
  script="perfect_hash.py"
  inputs = [
    "css/parser/at_rule_descriptor_id.json5",
  ]
  args = [
    rebase_path(inputs[0]),  # input file path
    rebase_path("$target_gen_dir/css/parser"),
  ]
  outputs = [
    "$target_gen_dir/css/parser/at_rule_descriptor_id_enum.h",
    "$target_gen_dir/css/parser/at_rule_descriptor_id_hash.template",
    "$target_gen_dir/css/parser/at_rule_descriptor_id_hash.cpp",
  ]
}