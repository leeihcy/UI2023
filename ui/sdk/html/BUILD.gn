
source_set("html") {
  sources = [
    "base/memory.h",
    "base/memory.cpp",

    "css/parser/css_parser_token.h",
    "css/parser/css_parser_token.cpp",
    "css/parser/css_parser_tokenizer.h",
    "css/parser/css_parser_tokenizer.cpp",
    "css/parser/css_parser_token_stream.h",
    "css/parser/css_parser_token_stream.cpp",
    "css/parser/css_property_parser.h",
    "css/parser/css_property_parser.cpp",
    "css/parser/css_parser.h",
    "css/parser/css_parser.cpp",

    "css/property/css_property.h",
    "css/property/css_property.cpp",
    "css/property/css_property_instances.h",
    "css/property/css_property_instances.cpp",
    "css/property/css_parsing_utils.h",
    "css/property/css_parsing_utils.cpp",
    "css/property/css_value.h",
    "css/property/css_value.cpp",

    "css/property/property_id.h",
    "css/property/property_id.cpp",
    "$target_gen_dir/css/property/property_id_hash.cpp",

    "css/property/value_id.h",
    "css/property/value_id.cpp",
    "$target_gen_dir/css/property/value_id_hash.cpp",

    "css/graphics/color.h",
    "css/graphics/color.cpp",
    "css/style_property_shorthand.h",
    "css/style_property_shorthand.cpp",

    "util/util.h",
    "util/util.cpp",
  ]

  if (is_debug) {
    sources += [
      "test/html_unittest_css_parser_token.cpp",
      "test/html_unittest_css_parser.cpp",
      "test/html_unittest_css_property.cpp",
      "test/html_unittest_memory.cpp",
      "test/html_unittest.cpp",
    ]
  }

  include_dirs=[
    "//ui/sdk"
  ]

  deps=[
    ":gen_property_id_hash",
    ":gen_value_id_hash",
  ]
}

# 生成property name->id 的完美Hash
action("gen_property_id_hash") {
  script="css/property/gen_property_hash.py"
  args = [
    rebase_path("$target_gen_dir/css/property")
  ]
  inputs = [
    "css/property/property_id.h",
  ]
  outputs = [
    "$target_gen_dir/css/property/property_id.template",
    "$target_gen_dir/css/property/property_id_hash.cpp",
  ]
}
# 生成property value->id 的完美Hash
action("gen_value_id_hash") {
  script="css/property/gen_value_hash.py"
  args = [
    rebase_path("$target_gen_dir/css/property")
  ]
  inputs = [
    "css/property/value_id.h",
  ]
  outputs = [
    "$target_gen_dir/css/property/value_id.template",
    "$target_gen_dir/css/property/value_id_hash.cpp",
  ]
}